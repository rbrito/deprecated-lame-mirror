dnl $Id$
dnl
dnl Process this file with autoconf to produce a configure script.
dnl
dnl include support and option for
dnl - frontend code: 
dnl -    VBR histogram  (optional)  done.
dnl -    SNDLIB         (lame or libsndfile required) done.
dnl - libmp3lame:
dnl -    VORBIS         done.
dnl -    decoder        done.
dnl -    disable shared done.
dnl -    disable analysis hooks -  why has this disappeared?
dnl -    shared lib version
dnl -    debugging mode
dnl -    MMX support
dnl - frame analyzer:
dnl -    make sure analysis hooks are enabled
dnl -    make sure decoder is enabled 
dnl -    check for GTK code  
dnl
dnl
dnl don't forget to set ASM_FOR_ARCH to a space delimited list of
dnl processor architectures, for which assembler routines exist
dnl
dnl
dnl Exported and configured variables:
dnl PGM
dnl CC
dnl CFLAGS
dnl CONFIG_DEFS
dnl LDFLAGS
dnl LDADD
dnl NASM

dnl extra vars for frontend:
dnl FRONTEND_LDFLAGS
dnl FRONTEND_CFLAGS
dnl FRONTEND_LDADD

dnl tools:
dnl MAKEDEP
dnl RM_F

AC_INIT(libmp3lame/lame.c)

dnl check system
AC_CANONICAL_SYSTEM
AC_VALIDATE_CACHED_SYSTEM_TUPLE

dnl automake
AM_INIT_AUTOMAKE(lame,3.88)
dnl AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
cflags_safe="${CFLAGS}"
AM_PROG_LIBTOOL
CFLAGS="${cflags_safe}"

# increase this when the shared lib becomes totally incompatible
LIB_MAJOR_VERSION=0

# increase this when changes are made, but they are upward compatible
# to previous versions
LIB_MINOR_VERSION=0

dnl Checks for programs.
dnl AC_PATH_PROG(AR,ar)

dnl check environment
AC_AIX
AC_ISC_POSIX
AC_MINIX

dnl more automake stuff
AM_PROG_CC_STDC
AM_C_PROTOTYPES

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gettimeofday socket strtol)


dnl Initialize configuration variables for the Makefile
CFLAGS=${CFLAGS}
NASM=
INCLUDES="-I\$(top_srcdir)/include -I\$(srcdir)"
FRONTEND_LDFLAGS=
FRONTEND_CFLAGS=
LIB_SOURCES=
MAKEDEP="-M"
RM_F="rm -f"
ASM_FOR_ARCH="i386"

dnl Checks for libraries.

dnl TODO: are the functions of termcap and curses the same as for ncurses ?
AC_CHECK_LIB(termcap, initscr, HAVE_TERMCAP="termcap")
AC_CHECK_LIB(curses, initscr, HAVE_TERMCAP="curses")
AC_CHECK_LIB(ncurses, initscr, HAVE_TERMCAP="ncurses")
 
dnl math lib
AC_CHECK_LIB(m, cos, USE_LIBM="-lm")
dnl free fast math library
AC_CHECK_LIB(ffm, cos, USE_LIBM="-lffm")
dnl Compaq fast math library.
AC_CHECK_LIB(cpml, cos, USE_LIBM="-lcpml")
CONFIG_MATH_LIB="${USE_LIBM}"

AC_CHECK_LIB(sndfile, sf_open_read, HAVE_SNDFILE="yes")
AC_CHECK_LIB(vorbis, ogg_sync_init, HAVE_VORBIS="yes")

dnl configure use of features

AM_PATH_GTK(1.2.0, HAVE_GTK="yes", HAVE_GTK="no")

dnl libsndfile
AC_MSG_CHECKING(use of file io)
WARNING=
AC_ARG_WITH(fileio, 
  [  --with-fileio=lame         Use lame's internal file io routines [default]]
  [             =sndfile      Use Erik de Castro Lopo's libsndfile]
  [                           (no stdin possible currently)],
  CONFIG_FILEIO="${withval}", CONFIG_FILEIO="lame")
AC_ARG_WITH(sndfile-prefix,
  [  --with-sndfile-prefix=DIR  Alternate location for libsndfile],
  CONFIG_SNDFILE_PREFIX="${withval}", CONFIG_SNDFILE_PREFIX="")
if test "${CONFIG_FILEIO}" = "sndfile" ; then
  if test "x${CONFIG_SNDFILE_PREFIX}" != "x" ; then 
    # look for sndfile lib. This overrides any standard location
    LA_SEARCH_LIB(SNDFILE_LIB_LOC, SNDFILE_INC_LOC, \
      libsndfile.a, sndfile.h, $CONFIG_SNDFILE_PREFIX) 
    if test "x${SNDFILE_LIB_LOC}" != "x" ; then
      HAVE_SNDFILE="yes"
      FRONTEND_LDFLAGS="-L${SNDFILE_LIB_LOC} ${FRONTEND_LDFLAGS}"
      INCLUDES="-I${SNDFILE_INC_LOC} ${INCLUDES}"
    else
      WARNING="sndfile lib not found in specified prefix."
    fi
  fi
  if test "${HAVE_SNDFILE}" = "yes" ; then
    FRONTEND_LDADD="-lsndfile ${FRONTEND_LDADD}"
    CONFIG_DEFS="-DLIBSNDFILE ${CONFIG_DEFS}"
  else
    # default
    CONFIG_FILEIO="lame"
    WARNING="${WARNING} Could not find any sndfile lib on system."
  fi
else
  CONFIG_FILEIO="lame"
fi 
AC_MSG_RESULT(${CONFIG_FILEIO})
if test "x${WARNING}" != "x" ; then
  AC_MSG_WARN($WARNING)
fi


dnl check if we should remove hooks for analyzer code in library
CONFIG_DEFS="-DANALYSIS ${CONFIG_DEFS}"


dnl mpg123 decoder
AC_MSG_CHECKING(use of mpg123 decoder)
AC_ARG_ENABLE(decoder, 
  [  --disable-decoder          Exclude mpg123 decoder],
  CONFIG_DECODER="${enableval}", CONFIG_DECODER="yes")
AC_ARG_ENABLE(decode-layer1, 
  [  --disable-decoder-layer1   Exclude layer1 decoding],
  CONFIG_DECODER_L1="${enableval}", CONFIG_DECODER_L1="yes")
AC_ARG_ENABLE(decode-layer1, 
  [  --disable-decoder-layer2   Exclude layer2 decoding],
  CONFIG_DECODER_L2="${enableval}", CONFIG_DECODER_L2="yes")

AM_CONDITIONAL(LIB_WITH_DECODER, test "x${CONFIG_DECODER}" = "xyes")

if test "${CONFIG_DECODER}" != "no" ; then
  CONFIG_DECODER="yes (Layer"
  # todo: add code to include this in libmp3lame  
  CONFIG_DEFS="-DHAVEMPGLIB ${CONFIG_DEFS}"
  
  if test "${CONFIG_DECODER_L1}" != "no"; then
    CONFIG_DEFS="-DUSE_LAYER_1 ${CONFIG_DEFS}"
    CONFIG_DECODER="${CONFIG_DECODER} 1,"
  fi
  if test "${CONFIG_DECODER_L2}" != "no"; then
    CONFIG_DEFS="-DUSE_LAYER_2 ${CONFIG_DEFS}"
    CONFIG_DECODER="${CONFIG_DECODER} 2,"
  fi
  CONFIG_DECODER="${CONFIG_DECODER} 3)"
fi
AC_MSG_RESULT($CONFIG_DECODER)


dnl vorbis
AC_MSG_CHECKING(use of vorbis)
WARNING=
AC_ARG_WITH(vorbis, 
  [  --with-vorbis              Use vorbis library for ogg files]
  [                           [default: yes, if installed]],
  CONFIG_VORBIS="${withval}", CONFIG_VORBIS="yes")
AC_ARG_WITH(vorbis-prefix,
  [  --with-vorbis-prefix=DIR   Alternate location for vorbis],
  CONFIG_VORBIS_PREFIX="${withval}", CONFIG_VORBIS_PREFIX="")
if test "${CONFIG_VORBIS}" != "no" ; then
  if test "x${CONFIG_VORBIS_PREFIX}" != "x" ; then
    # look for vorbis lib. This overrides any standard location
    LA_SEARCH_LIB(VORBIS_LIB_LOC, VORBIS_INC_LOC, \
      libvorbis.a, vorbis/codec.h, ${CONFIG_VORBIS_PREFIX})
    if test "x${VORBIS_LIB_LOC}" != "x" ; then
      HAVE_VORBIS="yes"
      FRONTEND_LDFLAGS="-L${VORBIS_LIB_LOC} ${FRONTEND_LDFLAGS}"
      INCLUDES="-I${VORBIS_INC_LOC} ${INCLUDES}"
    else
      WARNING="vorbis lib not found in specified prefix."
    fi
  fi
  if test "${HAVE_VORBIS}" = "yes" ; then
    FRONTEND_LDFLAGS="-lvorbis ${FRONTEND_LDFLAGS}"
    CONFIG_DEFS="-DHAVEVORBIS ${CONFIG_DEFS}"
  else
    CONFIG_VORBIS="no"
  fi
fi
AC_MSG_RESULT(${CONFIG_VORBIS})
if test "x${WARNING}" != "x" ; then
  AC_MSG_WARN($WARNING)
fi



AC_MSG_CHECKING(if mp3x is requested)
AC_ARG_ENABLE(mp3x,
  [  --enable-mp3x              Build GTK frame analyzer [default: no]],
    WITH_MP3X="mp3x", WITH_MP3X=)
if test "x${WITH_MP3X}" = "xmp3x"; then
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi

if test "${HAVE_GTK}" = "no"; then
	if test "x${WITH_MP3X}" = "xmp3x"; then
		AC_MSG_WARN(can't build mp3x, no GTK installed)
		WITH_MP3X=
	fi
fi

AC_MSG_CHECKING(if mp3rtp is requested)
AC_ARG_ENABLE(mp3rtp,
  [  --enable-mp3rtp            Build mp3rtp [default: no]],
    WITH_MP3RTP="mp3rtp", WITH_MP3RTP=)
if test "x${WITH_MP3RTP}" = "xmp3rtp"; then
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi


#
# this is from vorbis
#
dnl check GLIBC
case $target in 
*86-*-linux*)
	# glibc < 2.1.3 has a serious FP bug in the math inline header
	# that will cripple Vorbis.  Look to see if the magic FP stack
	# clobber is missing in the mathinline header, thus indicating
	# the buggy version

	AC_EGREP_CPP(log10.*fldlg2.*fxch,[
		#define __LIBC_INTERNAL_MATH_INLINES 1
	     	#define __OPTIMIZE__
		#include <math.h>
		],bad=maybe,bad=no)

	AC_MSG_CHECKING(glibc mathinline bug)
	if test ${bad} = "maybe" ;then
	      AC_EGREP_CPP(log10.*fldlg2.*fxch.*st\([[0123456789]]*\),
				[
				#define __LIBC_INTERNAL_MATH_INLINES 1
			     	#define __OPTIMIZE__
				#include <math.h>
				],bad=no,bad=yes)
	fi
	AC_MSG_RESULT(${bad})
	if test ${bad} = "yes" ;then
 AC_MSG_WARN([                                                        ])
 AC_MSG_WARN([********************************************************])
 AC_MSG_WARN([* The glibc headers on this machine have a serious bug *])
 AC_MSG_WARN([* in /usr/include/bits/mathinline.h  This bug affects  *])
 AC_MSG_WARN([* all floating point code, not only LAME, but all code *])
 AC_MSG_WARN([* built on this machine. Upgrading to glibc 2.1.3 is   *])
 AC_MSG_WARN([* strongly urged to correct the problem.               *])
 AC_MSG_WARN([*Note: that upgrading glibc will not fix any previously*])
 AC_MSG_WARN([* built programs; this is a compile-time time bug.     *])
 AC_MSG_WARN([* To work around the problem for this build of LAME,   *])
 AC_MSG_WARN([* autoconf is disabling all math inlining.  This will  *])
 AC_MSG_WARN([* hurt LAME performace but is necessary for LAME to    *])
 AC_MSG_WARN([* work correctly.  Once glibc is upgraded, rerun       *])
 AC_MSG_WARN([* configure and make to build with inlining.           *])
 AC_MSG_WARN([********************************************************])
 AC_MSG_WARN([                                                        ])

	CONFIG_DEFS="-D__NO_MATH_INLINES $CONFIG_DEFS"
	fi;;
esac


dnl configure use of VBR bitrate histogram
dnl todo: always use yes as default, use simulation instead ?
AC_MSG_CHECKING(use of VBR bitrate histogram)
if test "x${HAVE_TERMCAP}" != "x"; then
  BRH_DEFAULT="yes"
else
  BRH_DEFAULT="no"
fi
AC_ARG_ENABLE(brhist, 
  [  --enable-brhist            Include the VBR bitrate histogram feature]
  [                           [default: yes if any termcap available]],
  CONFIG_BRHIST="${enableval}", CONFIG_BRHIST="${BRH_DEFAULT}")
if test "${CONFIG_BRHIST}" != "no" ; then
  AM_CONDITIONAL(WITH_BRHIST, test "${CONFIG_BRHIST}" != "no")
  if test "${BRH_DEFAULT}" = "yes" ; then
    FRONTEND_LDADD="-l${HAVE_TERMCAP} ${FRONTEND_LDADD}"
    CONFIG_DEFS="-DBRHIST ${CONFIG_DEFS}"
    CONFIG_BRHIST="yes, with ${HAVE_TERMCAP}"
  else
    CONFIG_DEFS="-DBRHIST -DNOTERMCAP ${CONFIG_DEFS}"
    CONFIG_BRHIST="yes, simulated termcap"
  fi
fi
AC_MSG_RESULT(${CONFIG_BRHIST})


# some alternate code (work in progress Robert.Hegemann@gmx.de)
#CONFIG_DEFS="-DRH_AMP -DRH_VALIDATE_MS $CONFIG_DEFS"
#CONFIG_DEFS="-DKLEMM_01 $CONFIG_DEFS"


dnl ### processor specific options ###
case $target in
*86*-*-*)
	CPUTYPE="i386"
	;;
*)
	CPUTYPE="no"
	;;
esac



AC_PATH_PROG(NASM, nasm, no)
case "${NASM}" in
no)
	;;
*)
	AC_MSG_CHECKING(for assembler routines for this processor type)
	for recurse_over in ${ASM_FOR_ARCH}
	{
		if test "${CPUTYPE}" = "${recurse_over}"; then
			include_asm_routines="yes"
		fi
	}
	if test "x${include_asm_routines}" = "xyes"; then
		ENCODER_DEFS="-DHAVE_NASM -DMMX_choose_table"
	else
		include_asm_routines="no"
		NASM="no"
	fi
	AC_MSG_RESULT(${include_asm_routines})
	;;
esac
AM_CONDITIONAL(HAVE_NASM, test "${NASM}" != "no")


AC_MSG_CHECKING(for additional optimizations)
AC_ARG_ENABLE(expopt,
  [  --enable-expopt=full,norm  Whether to enable experimental optimizations]
  [                           [default: no]],   
    CONFIG_EXPOPT="${enableval}", CONFIG_EXPOPT="no")

if test "x$GCC" = "xyes"; then
	# gcc defaults. OS specific options go in versious sections below
	CFLAGS="-pedantic -Wall -pipe ${CFLAGS}"

	OPTIMIZATION="-O3 -fomit-frame-pointer -ffast-math -funroll-loops"

	OPTIMIZATION_NORM="-fthread-jumps -malign-double -frerun-loop-opt \
		-fgcse -fexpensive-optimizations"

	OPTIMIZATION_FULL="-fdelayed-branch -fmove-all-movables \
		-fschedule-insns2 -freduce-all-givs"

	case "${CONFIG_EXPOPT}" in
	no)
		;;
	norm|yes)
		OPTIMIZATION="${OPTIMIZATION} ${OPTIMIZATION_NORM}"
		;;
	full)
		OPTIMIZATION="${OPTIMIZATION} ${OPTIMIZATION_NORM} \
			${OPTIMIZATION_FULL}"
		;;
	*)
		AC_MSG_ERROR(bad value »${CONFIG_EXPOPT}« for expopt option)
		;;
	esac

	# some hardware dependend options
#	case ${target} in
#	*586*-*-*)
#		OPTIMIZATION="${OPTIMIZATION} -mpentium -march=pentium \
#			-mfancy-math-387"
#		;;
#	*686*-*-*)
#		OPTIMIZATION="${OPTIMIZATION} -mpentiumpro -march=pentiumpro \
#			-mfancy-math-387"
#		;;
#	*)
#		;;
#	esac

	AC_MSG_RESULT(${CONFIG_EXPOPT})
else
	AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING(for debug options)
AC_ARG_ENABLE(debug,
  [  --enable-debug=alot,norm   Enable debugging (disables optimizations)]
  [                           [default: no]],
    CONFIG_DEBUG="${enableval}", CONFIG_DEBUG="no")

if test "x$GCC" = "xyes"; then
	DEBUG_NO_DEFS="-DNDEBUG"
	DEBUG_NORM_DEFS="-UNDEBUG -DABORTFP"
	DEBUF_NORM_OPT="-O -g"
	DEBUG_ALOT_DEFS="-DDEBUG"

	case "${CONFIG_DEBUG}" in
	no)
		CONFIG_DEFS="${CONFIG_DEFS} ${DEBUG_NO_DEFS}"
		;;
	norm|yes)
		CONFIG_DEFS="${CONFIG_DEFS} ${DEBUG_NORM_DEFS}"
		OPTIMIZATION="${DEBUG_NORM_OPT}"
		;;
	alot)
		CONFIG_DEFS="${CONFIG_DEFS} ${DEBUG_NORM_DEFS} \					${DEBUG_ALOT_DEFS}"
		OPTIMIZATION="${DEBUG_NORM_OPT}"
		;;
	*)
		AC_MSG_ERROR(bad value »${CONFIG_DEBUG}« for debug option)
	esac

	AC_MSG_RESULT(${CONFIG_DEBUG})
else
	AC_MSG_RESULT(no)
fi



dnl ###  system specific options  ###

case $target in
*-*-linux*)

##########################################################################
# LINUX on Digital/Compaq Alpha CPUs
##########################################################################
case $target in
alpha*-*-linux*)

################################################################
#### Check if 'ccc' is in our path
################################################################
if test "`which ccc 2>/dev/null | grep -c ccc`" = ".0" ; then
#   if not, use 'gcc'
	# double is faster than float on Alpha
	dnl CONFIG_DEFS="-DFLOAT=double $CONFIG_DEFS"

	OPTIMIZATION="-O3 -fomit-frame-pointer -ffast-math -funroll-loops \
                -mfp-regs -fschedule-insns -fschedule-insns2"
	# add "-mcpu=21164a -Wa,-m21164a" to optimize for 21164a (ev56) CPU
else
	# Compaq's C Compiler
	CC=ccc

################################################################
#### set 'OPTIMIZATION = -arch host -tune host' 
####              to generate/tune instructions for this machine
####     'OPTIMIZATION += -migrate -fast -inline speed -unroll 0' 
####              tweak to run as fast as possible :)
####     'OPTIMIZATION += -w0' 
####              set warning and linking flags
################################################################
	OPTIMIZATION="-arch host -tune host"
	OPTIMIZATION="-migrate -fast -inline speed -unroll 0 $OPTIMIZATION"
	OPTIMIZATION="-w0 $OPTIMIZATION"


################################################################
#### to debug, uncomment
################################################################
	# For Debugging
	#OPTIMIZATION="-g3 $OPTIMIZATION"

################################################################
#### define __DECALPHA__ (i was getting re-declaration warnings
####   in machine.h
################################################################
	# Define DEC Alpha
	CONFIG_DEFS="-D__DECALPHA__ $CONFIG_DEFS"

fi  #  gcc or ccc?
;; # alpha

esac  
;; #  linux

##########################################################################
# SunOS
##########################################################################
sparc-*-sunos4*)
	CC="cc"
	OPTIMIZATION="-O -xCC"
	MAKEDEP="-xM"
	# for gcc, use instead:
	#   CC="gcc"
	#   OPTIMIZATION="-O"
	#   MAKEDEP="-M"
	endif
;; #SunOS

##########################################################################
# SGI
##########################################################################
*-sgi-irix*)
	CC="cc"
	OPTIMIZATION="-O3 -woff all"
;; # SGI

##########################################################################
# Compaq Alpha running Dec Unix (OSF)
##########################################################################
alpha*-dec-osf*)
	CC = cc
	OPTIMIZATION="-fast -O3 -std -g3 -non_shared"
;; #OSF

##########################################################################
# BeOS
##########################################################################
*-*-beos*) # todo:is this the right string ?
	CC="\$(BE_C_COMPILER)"
	case $target in
	bepc*-*-*) # todo: is this right architecture ? was: if ($(ARCH),BePC)
	   OPTIMIZATION="-O9 -fomit-frame-pointer -march=pentium \
			-mcpu=pentium -ffast-math -funroll-loops \
			-fprofile-arcs -fbranch-probabilities"
	;;
   	*) OPTIMIZATION="-opt all"
	   MAKEDEP="-make"
	;;
	esac
;; #BeOS
esac

# todo: include the following tests in the case-list
UNAME=`uname`
ARCH=`uname -m`

###########################################################################
# MOSXS (Rhapsody PPC)
###########################################################################
if test "$UNAME" = "Rhapsody"; then
   CC="cc"
   MAKEDEP="-make"
fi

##########################################################################
# OS/2
##########################################################################
# Properly installed EMX runtime & development package is a prerequisite.
# tools I used: make 3.76.1, uname 1.12, sed 2.05, PD-ksh 5.2.13
#
##########################################################################
if test "$UNAME" = "OS/2"; then
   SHELL=sh
   CC=gcc

   # file extension should be handled by automake (I don't know, please
   # give feedback!
   #FILE_EXTENSION=".exe"

# Uncomment & inspect the GTK lines to use MP3x GTK frame analyzer.
# Properly installed XFree86/devlibs & GTK+ is a prerequisite.
# The following works for me using Xfree86/OS2 3.3.5 and GTK+ 1.2.3:
#   CONFIG_DEFS="-DHAVEGTK -D__ST_MT_ERRNO__ $CONFIG_DEFS"
#   INCLUDES="-IC:/XFree86/include/gtk12 -IC:/XFree86/include/glib12 \
#             -IC:/XFree86/include $INCLUDES"
#   FRONTEND_LDFLAGS="-LC:/XFree86/lib -lgtk12 -lgdk12 -lgmodule -lglib12 \
#             -lXext -lX11 -lshm -lbsd -lsocket -lm $FRONTEND_LDFLAGS"
#   FRONTEND_CFLAGS="-Zmtd -Zsysv-signals -Zbin-files $FRONTEND_CFLAGS"
fi

###########################################################################
# MSDOS/Windows
###########################################################################
if test "$UNAME" = "MSDOS"; then
  # file extension should be handled by automake (I don't know, please
  # give feedback!
  #FILE_EXTENSION=".exe"
  RM_F=
fi

###########################################################################
# AmigaOS
###########################################################################
# Type 'Make ARCH=PPC' for PowerUP and 'Make ARCH=WOS' for WarpOS
#
###########################################################################
if test "$UNAME" = "AmigaOS" ; then
	CC="gcc -noixemul"
	OPTIMIZATION=$OPTIMIZATION -m68020-60 -m68881
	MAKEDEP="-MM"
	if test "$ARCH" = "WOS"; then
		CC="ppc-amigaos-gcc -warpup"
		OPTIMIZATION=$OPTIMIZATION -mmultiple -mcpu=603e"
		AR="ppc-amigaos-ar"
		RANLIB="ppc-amigaos-ranlib"
	fi
	if test "$ARCH",PPC; then
		CC="ppc-amigaos-gcc"
		OPTIMIZATION=$OPTIMIZATION -mmultiple -mcpu=603e"
		AR="ppc-amigaos-ar"
		RANLIB="ppc-amigaos-ranlib"
	fi
fi


CFLAGS="${OPTIMIZATION} ${CFLAGS}"
LDADD="${CONFIG_MATH_LIB} ${LDADD}"



AC_SUBST(INCLUDES)

AC_SUBST(FRONTEND_LDFLAGS)
AC_SUBST(FRONTEND_CFLAGS)
AC_SUBST(FRONTEND_LDADD)
AC_SUBST(LDADD)

AC_SUBST(LIB_MAJOR_VERSION)
AC_SUBST(LIB_MINOR_VERSION)

AC_SUBST(NASM)

dnl AC_SUBST(AR)
AC_SUBST(MAKEDEP)
AC_SUBST(RM_F)
AC_SUBST(CONFIG_DEFS)
AC_SUBST(ENCODER_DEFS)

AC_SUBST(LIBTOOL_DEPS)

AC_SUBST(WITH_MP3X)
AC_SUBST(WITH_MP3RTP)

AC_SUBST(CPUTYPE)


AC_OUTPUT(Makefile libmp3lame/Makefile libmp3lame/i386/Makefile frontend/Makefile mpglib/Makefile doc/Makefile doc/html/Makefile doc/man/Makefile include/Makefile Dll/Makefile misc/Makefile)
